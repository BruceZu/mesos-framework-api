apply plugin: 'application'
apply plugin: 'wrapper'

mainClassName = 'com.github.kevints.mesos.scheduler.demo.DemoMain'
applicationDefaultJvmArgs = ["-Djava.util.logging.config.file=${rootDir}/src/main/resources/logging.properties"]

wrapper {
  gradleVersion = '2.0'
}

dependencies {
  compile project(':mesos-scheduler-demo')
  compile 'org.slf4j:slf4j-jdk14:1.7.6'
}

allprojects {
  configurations.all {
    resolutionStrategy {
      failOnVersionConflict()
    }
  }

  apply plugin: 'java'
  apply plugin: 'idea'

  group 'com.github.kevints.mesos'
  version = '0.19-SNAPSHOT'

  repositories {
    mavenCentral()
  }
  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }

  ext.thirdparty = [
    junit: 'junit:junit:4.11',
    googleHttpClient: 'com.google.http-client:google-http-client:1.18.0-rc',
    guava: 'com.google.guava:guava:17.0',
    protobuf: 'com.google.protobuf:protobuf-java:2.5.0'
  ]
}

subprojects {
  apply plugin: 'maven-publish'
  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
      }
    }
  }
}
